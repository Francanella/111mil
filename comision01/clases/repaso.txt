---------------------------------------------------------------------
      Repaso
---------------------------------------------------------------------
Algoritmo SubAlgoritmoGuiones
  Escribir guiones(40)
  Escribir guiones(10),"Pepe",guiones(10)
FinAlgoritmo

SubAlgoritmo retorno = guiones(cantidad)
  Definir retorno Como Caracter
  Definir i Como Entero
  Para i = 1 Hasta cantidad Con Paso 1 Hacer
    retorno = retorno + "-"
  FinPara
FinSubAlgoritmo


---------------------------------------------------------------------
Algoritmo SubAlgoritmoDiasDeLaSemanaPrueba
  Escribir "Hoy es: ", diaDeLaSemana(4)
  Escribir diaDeLaSemana(10)
FinAlgoritmo

SubAlgoritmo textoDia = diaDeLaSemana(numeroDia)
  Definir textoDia Como Caracter
  Segun numeroDia hacer
    1: textoDia = "lunes"
    2: textoDia = "martes"
    3: textoDia = "miércoles"
    4: textoDia = "jueves"
    5: textoDia = "viernes"
    6: textoDia = "sábado"
    7: textoDia = "domingo"
    De Otro Modo:
      textoDia = "ERROR: DIA NO VALIDO!"
  FinSegun
FinSubAlgoritmo

---------------------------------------------------------------------
Algoritmo PruebaSubAlgoritmoPotencia
  Escribir potencia(2,10)
  Escribir potencia(3,6)
  Escribir potencia(2,20)
  Escribir potencia(2,30)
  Escribir potencia(19,0)
FinAlgoritmo

SubAlgoritmo resultado = potencia(base, exponente)
  Definir resultado Como Entero
  Definir i Como Entero
  
  Si exponente = 0 Entonces
    resultado = 1
  SiNo
    resultado = base
    Para i=2 Hasta exponente Hacer
      resultado = resultado * base 
    FinPara   
  FinSi

FinSubAlgoritmo

---------------------------------------------------------------------
Algoritmo PruebaSubAlgoritmoEsMultiplo
  Escribir esMultiplo(10, 2)
  Escribir esMultiplo(11, 3)
  Escribir esMultiplo( 2, 8)
FinAlgoritmo

SubAlgoritmo resultado = esMultiplo(multiplo, factor)
  Definir resultado Como Logico
  Si (multiplo mod factor) = 0 Entonces
    resultado = Verdadero
  SiNo
    resultado = Falso
  FinSi
FinSubAlgoritmo

---------------------------------------------------------------------
Algoritmo PruebaSubAlgoritmoCantidadDeDivisores
  Escribir cantidadDeDivisores(16)
  Escribir cantidadDeDivisores(10)
  Escribir cantidadDeDivisores(54)
  Escribir cantidadDeDivisores(100)
  Escribir cantidadDeDivisores(19)
  Escribir cantidadDeDivisores(1013)
FinAlgoritmo

SubAlgoritmo resultado = esMultiplo(multiplo, factor)
  Definir resultado Como Logico
  Si (multiplo mod factor) = 0 Entonces
    resultado = Verdadero
  SiNo
    resultado = Falso
  FinSi
FinSubAlgoritmo

SubAlgoritmo resultado = cantidadDeDivisores(num)
  Definir resultado como entero
  Definir i como entero
  
  resultado = 0
  para i = 1 hasta num hacer
    si num mod i = 0 entonces
      resultado = resultado + 1
    FinSi
  FinPara
FinSubAlgoritmo
